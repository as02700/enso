from Base import all

type Expression
    # type Column (origin : Text) (name : Text)
    type Column origin name

    # type Constant (sql_type : SQL.SqlType) (value : Any)
    type Constant sql_type value

    # type Operation (kind : Text) (expressions : Vector Expression)
    type Operation kind expressions

type Context
    # type Context (from_spec : FromSpec) (where_filters : Vector Expression)
    #              (orders : Vector [Expression, OrderType, NullsOrder])
    #              (groups : Vector Expression) (meta_index : Vector Expression)
    type Context from_spec where_filters orders groups meta_index

    set_index : Vector Text -> Context
    set_index new_index =
        Context this.from_spec this.where_filters this.orders this.groups new_index

    set_where_filters : Vector Expression -> Context
    set_where_filters new_filters =
        Context this.from_spec new_filters this.orders this.groups this.meta_index

    # set_orders : Vector [Expression, OrderType] -> Context
    set_orders new_orders =
        Context this.from_spec this.where_filters new_orders this.groups this.meta_index

    set_groups : Vector Expression -> Context
    set_groups new_groups =
        Context this.from_spec this.where_filters this.orders new_groups this.meta_index

type FromSpec
    # type FromTable (table_name : Text) (alias : Text)
    type FromTable table_name alias
    # type Join (kind : JoinKind) (left_spec : FromSpec) (right_spec : FromSpec)
    #           (on : Vector Expression)
    type Join kind left_spec right_spec on
    # type SubQuery (columns : Vector [Text, Expression])
    #               (context : Context) (alias : Text)
    type SubQuery columns context alias

type JoinKind
    type InnerJoin
    type LeftJoin
    type RightJoin
    type CrossJoin

type OrderType
    type Ascending
    type Descending

type NullsOrder
    type NullsFirst
    type NullsLast

type Query
    # type Select (expressions : [Text, Expression]) (context : Context)
    type Select columns context

make_ctx_from : Text -> Context
make_ctx_from table_name =
    Context (FromTable table_name table_name) [] [] [] []

make_constant : SQL.SqlType -> Any -> Expression
make_constant sql_type x =
    # TODO [RW] may add some sanitization, like checking if the value type is supported
    Constant sql_type x

substitute_origin : Text -> Text -> Expression
substitute_origin old_origin new_origin expr = case expr of
    Column origin name ->
        if origin == old_origin then Column new_origin name else expr
    Constant _ _ -> expr
    Operation kind exprs ->
        Operation kind (exprs.map (here.substitute_origin old_origin new_origin))
